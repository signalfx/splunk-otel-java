import nebula.plugin.release.git.opinion.Strategies

plugins {
  id "java"
  id "com.diffplug.spotless" version "5.12.1" apply false
  id "nebula.release" version "15.3.1"
}

release {
  defaultVersionStrategy = Strategies.getSNAPSHOT()
}

group = "com.splunk.public"

subprojects {
  version = rootProject.version

  apply plugin: 'java'
  apply plugin: 'com.diffplug.spotless'
  apply from: "$rootDir/gradle/spotless.gradle"
  apply from: "$rootDir/gradle/dependencies.gradle"

  repositories {
    mavenLocal()
    maven {
      url = uri("https://dl.bintray.com/open-telemetry/maven")
    }
    mavenCentral()
    maven {
      url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
  }

  dependencies {
    testImplementation deps.assertj
    testImplementation deps.mockito
    testImplementation deps.junit
    testRuntimeOnly deps.junitEngine
  }

  tasks {
    test {
      useJUnitPlatform()
      reports {
        junitXml.outputPerTestCase = true
      }
    }
  }

  tasks.withType(JavaCompile) {
    options.deprecation = true
  }
}

