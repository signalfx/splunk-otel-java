import nebula.plugin.release.git.opinion.Strategies

plugins {
  id "java"
  id "com.diffplug.spotless" apply false
  id "nebula.release"
  id "io.github.gradle-nexus.publish-plugin"
}

release {
  defaultVersionStrategy = Strategies.SNAPSHOT
}

nebulaRelease {
  addReleaseBranchPattern(/v\d+\.\d+\.x/)
}

nexusPublishing {
  packageGroup = "com.splunk"

  repositories {
    sonatype {
      username = System.getenv('SONATYPE_USERNAME')
      password = System.getenv('SONATYPE_PASSWORD')
    }
  }
}

group = "com.splunk"

subprojects {
  version = rootProject.version

  apply plugin: 'java'
  apply plugin: 'com.diffplug.spotless'
  apply from: "$rootDir/gradle/spotless.gradle"
  apply from: "$rootDir/gradle/dependencies.gradle"

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
  }

  dependencies {
    testImplementation deps.assertj
    testImplementation deps.mockito
    testImplementation deps.junit
    testRuntimeOnly deps.junitEngine
  }

  tasks {
    test {
      useJUnitPlatform()
      reports {
        junitXml.outputPerTestCase = true
      }
    }
  }

  tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
  }
}

