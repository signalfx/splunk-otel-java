apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: "$rootDir/gradle/shadow.gradle"

def relocatePackages = ext.relocatePackages

configurations {
    testInstrumentation
    testAgent
}

dependencies {
    compileOnly("io.opentelemetry:opentelemetry-sdk:${versions.opentelemetry}")
    compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:${versions.opentelemetryJavaagentAlpha}")
    compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")
    compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-api:${versions.opentelemetryJavaagentAlpha}")
    compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-spi:${versions.opentelemetryJavaagentAlpha}")
    compileOnly("net.bytebuddy:byte-buddy:1.10.10")
    annotationProcessor("com.google.auto.service:auto-service:1.0-rc3")
    annotationProcessor("com.google.auto:auto-common:0.8")
    compileOnly("com.google.auto.service:auto-service:1.0-rc3")
    compileOnly("com.google.auto:auto-common:0.8")
    compileOnly(project(":bootstrap"))
    compileOnly("io.micrometer:micrometer-core:${versions.micrometer}")

    // test
    testImplementation(project(":testing:common"))
    testAgent(project(path: ":testing:agent-for-testing", configuration: "shadow"))
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath, project.configurations.testInstrumentation]
    mergeServiceFiles()

    archiveFileName = 'agent-testing.jar'

    relocatePackages(it)
}

evaluationDependsOn(":testing:agent-for-testing")

tasks.withType(Test).configureEach {
    dependsOn shadowJar
    dependsOn ":testing:agent-for-testing:shadowJar"

    jvmArgs "-Dotel.javaagent.debug=true"
    jvmArgs "-javaagent:${configurations.testAgent.files.first().absolutePath}"
    jvmArgs "-Dotel.javaagent.experimental.initializer.jar=${shadowJar.archiveFile.get().asFile.absolutePath}"
    jvmArgs "-Dotel.javaagent.testing.additional-library-ignores.enabled=false"
    jvmArgs "-Dotel.javaagent.testing.fail-on-context-leak=true"
    // prevent sporadic gradle deadlocks, see SafeLogger for more details
    jvmArgs "-Dotel.javaagent.testing.transform-safe-logging.enabled=true"

    // The sources are packaged into the testing jar so we need to make sure to exclude from the test
    // classpath, which automatically inherits them, to ensure our shaded versions are used.
    classpath = classpath.filter {
        if (file("$buildDir/resources/main").equals(it) || file("$buildDir/classes/java/main").equals(it)) {
            return false
        }
        return true
    }
}
