import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
  id 'war'
  id "com.bmuschko.docker-remote-api" version "6.6.1"
}

tasks {
  compileJava {
    options.release.set(8)
  }
}

def versions = ext['versions']

dependencies {
  implementation("javax.servlet:javax.servlet-api:3.0.1")
  implementation("io.opentelemetry:opentelemetry-extension-annotations:${versions["opentelemetry"]}")
}

def dockerWorkingDir = new File(project.buildDir, "docker")

def buildProprietaryTestImagesTask = tasks.create("buildProprietaryTestImages") {
  group = "build"
  description = "Builds all Docker images for the test matrix for proprietary app servers"
}

def targets = [
   "weblogic" : [
        "12.2.1.4" : ["developer"],
        "14.1.1.0" : ["developer-8", "developer-11"]
   ],
    "jboss-eap" : [
        "7.1.0" : ["8"],
        "7.3.0" : ["8", "11"]
    ]
]

targets.forEach { server, data ->
  String dockerFileName = "${server}.dockerfile"
  data.forEach { version, jdks ->
    jdks.forEach { jdk ->
      def prepareTask = tasks.register("${server}ImagePrepare-$version-jdk$jdk", Copy) {
        def warTask = project.tasks.named("war").get()
        it.dependsOn(warTask)
        it.into(dockerWorkingDir)
        it.from("src/${dockerFileName}")
        it.from("src/main/docker/${server}")
        it.from(warTask.archiveFile) {
          rename { _ -> "app.war" }
        }
      }

      def buildTask = tasks.register("${server}Image-$version-jdk$jdk", DockerBuildImage) {
        it.dependsOn(prepareTask)
        group = "build"
        description = "Builds Docker image with $server $version on JDK $jdk"

        it.inputDir = dockerWorkingDir
        it.images.add "splunk-$server:$version-jdk$jdk"
        it.buildArgs.set(["version": version, "jdk": jdk])

        it.dockerFile = new File(dockerWorkingDir, dockerFileName)
      }

      buildProprietaryTestImagesTask.dependsOn(buildTask)
    }
  }
}