plugins {
  id 'java'
  id "org.gradle.test-retry" version "1.2.0"
}

dependencies {
  testImplementation("org.testcontainers:testcontainers:1.15.0-rc2")
  testImplementation("com.fasterxml.jackson.core:jackson-databind:2.11.2")
  testImplementation("com.google.protobuf:protobuf-java-util:3.12.4")
  testImplementation("com.squareup.okhttp3:okhttp:3.12.12")
  testImplementation("io.opentelemetry:opentelemetry-proto:0.10.0")
  testImplementation("io.opentelemetry:opentelemetry-api:0.10.0")

  testImplementation("ch.qos.logback:logback-classic:1.2.3")
  testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.0")
}

tasks.test {
  useJUnitPlatform()
  maxParallelForks = 2
  retry {
    // You can see tests that were retried by this mechanism in the collected test reports and build scans.
    maxRetries = System.getenv("CI") != null ? 5 : 0
  }

  reports {
    junitXml.outputPerTestCase = true
  }

  def shadowTask = project(":agent").tasks.named("shadowJar").get()
  inputs.files(layout.files(shadowTask))

  doFirst {
    jvmArgs("-Dio.opentelemetry.smoketest.agent.shadowJar.path=${shadowTask.archiveFile.get()}")
  }
}