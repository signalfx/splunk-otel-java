import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id("splunk.shadow-conventions")
}

configurations {
  // dependencies that already are relocated and will be moved to inst/ (agent classloader isolation)
  isolateLibs
  // dependencies that will be relocated
  relocateLibs
  // dependencies that will be included as they are
  includeAsIs
}

dependencies {
  // include micrometer-core API
  relocateLibs(project(":bootstrap"))
  // include testing extensions, e.g. micrometer
  isolateLibs(project(path: ":testing:agent-test-extension", configuration: "shadow"))

  // and finally include everything from otel agent for testing
  //TODO remove this `@jar` when upstream sorts its publishing
  includeAsIs("io.opentelemetry.javaagent:opentelemetry-agent-for-testing@jar")
}

CopySpec isolateAgentClasses(Iterable<File> jars) {
  return copySpec {
    jars.each {
      from(zipTree(it)) {
        into("inst")
        rename("(^.*)\\.class\$", "\$1.classdata")
      }
    }
  }
}

task relocateAndIsolate(type: ShadowJar) {
  dependsOn ':testing:agent-test-extension:shadowJar'

  configurations = [project.configurations.relocateLibs]

  with isolateAgentClasses(project.configurations.isolateLibs.files)
}

shadowJar {
  from project.configurations.includeAsIs.files
  from tasks.relocateAndIsolate.outputs.files

  manifest {
    attributes(
        "Main-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
        "Agent-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
        "Premain-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
        "Can-Redefine-Classes": true,
        "Can-Retransform-Classes": true,
    )
  }

  mergeServiceFiles {
    include("inst/META-INF/services/**")
  }
}

jar {
  enabled = false
}

tasks.assemble.dependsOn(tasks.shadowJar)